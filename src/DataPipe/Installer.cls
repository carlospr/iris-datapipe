Class DataPipe.Installer Extends %RegisteredObject
{

Parameter Namespace = "DPIPE";

Parameter SourcePath = "/app/src";

Parameter CreateNamespace = "no";

Parameter DataDBPath;

Parameter CodeDBPath;

Parameter CreateWebApp = "yes";

Parameter WebApp;

Parameter StartTestProduction = "no";

XData Install [ XMLNamespace = INSTALLER ]
{
<Manifest> 
    <If Condition='"${CreateNamespace}"="yes"'>
        <Log Text="Creating namespace ${Namespace}" Level="0"/> 
        <Namespace Name="${Namespace}" Create="yes" Ensemble=""> 
            <Configuration> 
                <Database Name="${Namespace}-DATA" Dir="${DataDBPath}" Create="yes" MountRequired="true" Resource="%DB_DEFAULT" PublicPermissions="RW" MountAtStartup="true"/> 
                <Database Name="${Namespace}-APP" Dir="${CodeDBPath}" Create="yes" MountRequired="true" Resource="%DB_DEFAULT" PublicPermissions="RW" MountAtStartup="true"/> 
            </Configuration>
            <Import File="${SourcePath}" Recurse="1" Flags="cuk" />
        </Namespace>
    </If>
    <If Condition='"${CreateNamespace}"="no"'>
        <Log Text="Using namespace ${Namespace}" Level="0"/> 
        <Namespace Name="${Namespace}"> 
            <Import File="${SourcePath}" Recurse="1" Flags="cuk" />
        </Namespace>
    </If>
    <If Condition='"${StartTestProduction}"="yes"'>
        <Namespace Name="${Namespace}">
            <Production Name="DataPipe.Test.Production" AutoStart="1"/>
        </Namespace> 
    </If>
</Manifest>
}

/// This is a method generator whose code is generated by XGL. 
/// Main setup method
ClassMethod RunManifest(ByRef pVars, pLogLevel As %Integer = 0, pInstaller As %Installer.Installer) As %Status [ CodeMode = objectgenerator, Internal ]
{
    Quit ##class(%Installer.Manifest).%Generate(%compiledclass, %code, "Install")
}

/// Entry point    
ClassMethod Run(ByRef vars) As %Status
{
    try { 
        write "START INSTALLER",! 
        
        // set default values for installer variables
        for ivar="SourcePath","Namespace","CreateNamespace","CreateDB","DataDBPath","CodeDBPath","CreateWebApp","WebApp","EnableUnitTestPortal" {
            if '$data(vars(ivar)) set vars(ivar)=$parameter($classname(), ivar)
        } 
        set sc = ..RunManifest(.vars) 
        if $$$ISOK(sc) {
            write !,"INSTALLER SUCCESS"
        }
        $$$ThrowOnError(sc)

        // create web application
        if vars("CreateWebApp")="yes" { 
            $$$ThrowOnError(..CreateWebApp(vars("Namespace"), vars("WebApp"), "DataPipe.REST.Main"))
        }

    } catch ex { 
        set sc = ex.AsStatus() 
        write $System.Status.GetErrorText(sc),! 
        do $SYSTEM.Process.Terminate($JOB,1) 
    } 
    quit sc
}

/// Create web application
ClassMethod CreateWebApp(pNamespace As %String, pWebApp As %String, pDispatch As %String) As %Status
{
  set ret = $$$OK
  try {
    zn "%SYS" 
    set webName = pWebApp 
    set webProperties("DispatchClass") = pDispatch 
    set webProperties("NameSpace") = pNamespace 
    set webProperties("Enabled") = 1 
    set webProperties("AutheEnabled") = 32 
    $$$ThrowOnError(##class(Security.Applications).Create(webName, .webProperties))

  } catch ex {
    set ret = ex.AsStatus()
  } 
  quit ret
}

}
